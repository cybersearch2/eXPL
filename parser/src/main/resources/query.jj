options 
{
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
}

PARSER_BEGIN(QueryParser)
import java.io.InputStream;
import java.io.IOException;
import java.util.List;
import java.util.Map;
import java.util.ArrayList;
import java.util.HashMap;

import au.com.cybersearch2.classy_logic.Scope;
import au.com.cybersearch2.classy_logic.pattern.KeyName;
import au.com.cybersearch2.classy_logic.pattern.Template;
import au.com.cybersearch2.classy_logic.pattern.Choice;
import au.com.cybersearch2.classy_logic.query.QuerySpec;
import au.com.cybersearch2.classy_logic.QueryProgram;
import au.com.cybersearch2.classy_logic.compile.ParserContext;
import au.com.cybersearch2.classy_logic.compile.ParserAssembler;
import au.com.cybersearch2.classy_logic.compile.ParserResources;
import au.com.cybersearch2.classy_logic.compile.Group;
import au.com.cybersearch2.classy_logic.compile.OperandMap;
import au.com.cybersearch2.classy_logic.compile.OperandType;
import au.com.cybersearch2.classy_logic.compile.ParserTask;
import au.com.cybersearch2.classy_logic.compile.VariableType;
import au.com.cybersearch2.classy_logic.expression.BooleanOperand;
import au.com.cybersearch2.classy_logic.expression.DoubleOperand;
import au.com.cybersearch2.classy_logic.expression.StringOperand;
import au.com.cybersearch2.classy_logic.expression.NullOperand;
import au.com.cybersearch2.classy_logic.expression.IntegerOperand;
import au.com.cybersearch2.classy_logic.expression.RegExOperand;
import au.com.cybersearch2.classy_logic.expression.MatchOperand;
import au.com.cybersearch2.classy_logic.expression.Evaluator;
import au.com.cybersearch2.classy_logic.expression.LoopEvaluator;
import au.com.cybersearch2.classy_logic.expression.Variable;
import au.com.cybersearch2.classy_logic.expression.FormatterOperand;
import au.com.cybersearch2.classy_logic.expression.FactOperand;
import au.com.cybersearch2.classy_logic.expression.ChoiceOperand;
import au.com.cybersearch2.classy_logic.expression.LiteralListOperand;
import au.com.cybersearch2.classy_logic.expression.OperatorEnum;
import au.com.cybersearch2.classy_logic.list.AxiomOperand;
import au.com.cybersearch2.classy_logic.list.ItemListOperand;
import au.com.cybersearch2.classy_logic.list.ListLength;
import au.com.cybersearch2.classy_logic.terms.StringTerm;
import au.com.cybersearch2.classy_logic.terms.IntegerTerm;
import au.com.cybersearch2.classy_logic.terms.DoubleTerm;
import au.com.cybersearch2.classy_logic.terms.BooleanTerm;
import au.com.cybersearch2.classy_logic.terms.Parameter;
import au.com.cybersearch2.classy_logic.terms.NumberTerm;
import au.com.cybersearch2.classy_logic.interfaces.Term;
import au.com.cybersearch2.classy_logic.interfaces.Operand;
import au.com.cybersearch2.classy_logic.interfaces.ItemList;
import au.com.cybersearch2.classy_logic.interfaces.AxiomProvider;
import au.com.cybersearch2.classy_logic.helper.Null;
import au.com.cybersearch2.classy_logic.helper.Unknown;
import au.com.cybersearch2.classy_logic.helper.QualifiedName;
import au.com.cybersearch2.classy_logic.helper.QualifiedTemplateName;
import au.com.cybersearch2.classy_logic.helper.OperandParam;



/** 
 * QueryParser
 * JavaCC generated Expression Pattern Language Compiler 
 * @author Andrew Bowley
 * 30 Sep 2010
 */
public class QueryParser 
{
  
  /** 
   * Main entry point reads from System.in. Generates console output only. Use for validation. 
   * @throws ParseException
   */
  public static void main(String args[]) throws ParseException
  {
    QueryParser parser = new QueryParser(System.in);
    QueryProgram queryProgram = new QueryProgram();
    parser.input(queryProgram);
  }


  /** 
   * Returns compiled result of eXPL script from supplied input stream.
   * @param inputStream  InputStream
   * @return QueryProgram object
   * @throws ParseException
   */
  public QueryProgram parse(InputStream inputStream) throws ParseException
  {
    ReInit(inputStream);
    QueryProgram queryProgram = new QueryProgram();
    input(queryProgram);
    return queryProgram;
  }

  /**
   * Include eXPL script from named resource
   * @param resourceName Name of file or other resource to include
   * @param queryProgram QueryProgram object accumulating the compiled result
   * @throws ParseException
   */
  public void includeResource(String resourceName, QueryProgram queryProgram) throws ParseException
  {
    if (resourceName.length() < 3)
      throw new ParseException("Include resourceName \"\" is invalid");
    ParserResources parserResources = new ParserResources(queryProgram);
    try
    {
      parserResources.includeResource(resourceName.substring(1, resourceName.length() - 1));
    }
    catch (IOException e)
    {
      throw new ParseException(e.getMessage());
    }
  } 
 
  /**
   * Returns content of string literal token stipped of quote delimiters
   * @param stringLiteral Token object
   * @return String
   */
  protected String getText(Token stringLiteral)
  {
      return stringLiteral.image.substring(1, stringLiteral.image.length() - 1);
  } 

  protected Operand axiomOperand(String name, 
                                         ParserAssembler parserAssembler, 
                                         Operand indexExpression1, 
                                         Operand indexExpression2)
  {
    AxiomOperand operand = new AxiomOperand(name, indexExpression1, indexExpression2);
    ParserTask parserTask = parserAssembler.addPending(operand);
    //parserTask.setPriority(1);
    return operand;
  }
  
  protected Operand listItemOperand(String name, 
                                    ParserAssembler parserAssembler, 
                                    Operand indexExpression, 
                                    Operand assignExpression)
  {
    ItemListOperand operand = new ItemListOperand(name, indexExpression, assignExpression);
    ParserTask parserTask = parserAssembler.addPending(operand);
    //parserTask.setPriority(1);
    return operand;
  }
}
PARSER_END(QueryParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~["\n","\r"])* ("\n" | "\r" | "\r\n") >
}

TOKEN :
{
  < INTEGER: "integer" >
| < DOUBLE: "double" >
| < DECIMAL: "decimal" >
| < BOOLEAN: "boolean" >
| < STRING: "string" >
| < TEMPLATE: "template" >
| < AXIOM: "axiom" >
| < REGEX: "regex" >
| < INCLUDE: "include" >
| < SCOPE: "scope" >
| < QUERY: "query" >
| < CALC: "calc" >
| < LIST: "list" >
| < FACT: "fact" >
| < LENGTH: "length" >
| < TERM: "term" >
| < RESOURCE: "resource" >
| < CURRENCY: "currency" >
| < FORMAT: "format" >
| < LOCAL: "local" >
| < CHOICE: "choice" >
| < PARAMETER: "parameter" >
}

/* LITERALS */

TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL>
      | <HEX_LITERAL>
  >
|
  < #DECIMAL_LITERAL: (["0"-"9"])+ >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["d","D"])?
      | (["0"-"9"])+ (<EXPONENT> | (["d","D"]))
  >
|
  < NUMBER_LITERAL:
       "'" 
       ((["0"-"9"])+ ([",","."]))+ (["0"-"9"])+
       "'"
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < STRING_LITERAL:
      "\""
      (     
        (~["\"","\\","\n","\r"])
        | 
        ("\\" ( ["n","t","b","r","f","\\","'","\"","."] ) )
      )*
      "\""
  >
|
  < TRUE: "true" >
|
  < FALSE: "false" >
|
   <UNKNOWN: "unknown" >
|
   <NAN: "NaN" >
}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: ["a"-"z","A"-"Z"] (["_","a"-"z","A"-"Z","0"-"9"])* >
}


/* SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

/* OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < BANG: "!" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
}

/** Root production. */
void input(QueryProgram queryProgram) :
{
  ParserContext context = new ParserContext(queryProgram);
}
{
  ( ResourceDeclaration(context) ";" )*
  ( Statement(context) | QueryChain(context) | ScopeDeclaration(context) )+
  <EOF>
  {
    queryProgram.runPending();
  }
}

void ResourceDeclaration(ParserContext context) :
{
  Token nameToken;
  Map<String, Object> properties = new HashMap<String, Object>();
}
{
  <RESOURCE> nameToken=<STRING_LITERAL> ( "(" InitialiserList(properties) ")" )?
  {
    context.getQueryProgram().openResource(getText(nameToken), properties);
  }
}


void ScopeDeclaration(ParserContext context) :
{
}
{
  <SCOPE> Scope(context) 
  "{" 
     ( Statement(context) | QueryChain(context) )*
  "}" 
  {
    context.resetScope();
  }
}

void Scope(ParserContext context) :
{
  Token scopeToken;
  Map<String, Object> properties = new HashMap<String, Object>();
}
{
   scopeToken = <IDENTIFIER>  [ "(" InitialiserList(properties) ")" ]
   {
      context.setScope(context.getQueryProgram().scopeInstance(scopeToken.image , properties));
   }
}

void QueryChain(ParserContext context) :
{
  QuerySpec querySpec;
  Scope scope = context.getScope();
}
{  
  <QUERY> querySpec=Query() querySpec=QueryDeclaration(querySpec, scope) ( ">>" QueryDeclaration(querySpec.chain(), scope) )* ";"
  {
    scope.addQuerySpec(querySpec);
  }  
}

QuerySpec Query() :
{
   Token queryToken;
}
{
  queryToken=<IDENTIFIER>
  {
    return new QuerySpec(queryToken.image);
  }    
}

QuerySpec QueryDeclaration(QuerySpec querySpec, Scope scope) :
{
  KeyName firstKeyname;
  int keynameCount = 1;
  Map<String, Object> properties = new HashMap<String, Object>();
}
{
   "(" 
      ( 
        firstKeyname = KeyName(querySpec)
      )
      (
        "," 
        (
          KeyName(querySpec)
        )
        {
            ++keynameCount;
        }
      )* ")" ( "(" InitialiserList(properties) ")" )?
    {
        return scope.buildQuerySpec(querySpec, firstKeyname, keynameCount, properties);
    }
}

KeyName KeyName(QuerySpec querySpec) :
{
  String name1;
  String name2 = null;
}
{
  name1=Name() (":" name2=Name() )?
  {
    boolean isBinary = name2 != null;
    String axiomKey = isBinary  ? name1 : "";
    String templateName = isBinary  ? name2 : name1;
    KeyName keyname = new KeyName(axiomKey, templateName);
    querySpec.addKeyName(keyname);
    return keyname;
  }
}

void Statement(ParserContext context):
{
  Operand var;
}
{
    var=VariableDeclaration(context) ";" 
    {
      context.getOperandMap().addOperand(var);
    }
  | TemplateDeclaration(context) ";" 
  | CalculatorTemplate(context) ";"   
  | AxiomDeclaration(context) ";" 
  | ChoiceDeclaration(context) ";"
  | ListDeclaration(context) ";"
  | Include(context) ";"
}

void AxiomDeclaration(ParserContext context) :
{
  QualifiedName qualifiedAxiomName;
  Operand operand = null;
}
{
  <AXIOM> qualifiedAxiomName=Axiom(context) 
          ( AxiomSpecification(qualifiedAxiomName, context) 
            |
            operand=AxiomVariable(qualifiedAxiomName, context) 
          )
  {
    if (operand != null)
      context.getOperandMap().addOperand(operand);
  }
} 

QualifiedName Axiom(ParserContext context) :
{
  String axiomName;
}
{
  axiomName=Name()
  { 
    return axiomName.indexOf(".") == -1 ? 
        context.getParserAssembler().getContextName(axiomName) :
        QualifiedName.parseName(axiomName); 
  }
}

void AxiomSpecification(QualifiedName qualifiedAxiomName, ParserContext context) :
{
  ParserAssembler parserAssembler = context.getParserAssembler();
  parserAssembler.createAxiom(qualifiedAxiomName);
}
{
  "(" ( TermName(qualifiedAxiomName, parserAssembler) ("," TermName(qualifiedAxiomName, parserAssembler) )* )?  ")"  
  (
    ":" (ResourceBinding(QueryParserConstants.AXIOM, qualifiedAxiomName, parserAssembler)
         |
         ParameterDeclaration(qualifiedAxiomName, parserAssembler) )
    |
    ( <LBRACE> AxiomItem(qualifiedAxiomName, parserAssembler) <RBRACE> )+
  )?
}

void TermName(QualifiedName qualifiedAxiomName, ParserAssembler parserAssembler) :
{
  Token nameToken;
}
{
  nameToken=<IDENTIFIER>
  {
    parserAssembler.addAxiomTermName(qualifiedAxiomName, nameToken.image);
  }
}

QualifiedName ResourceBinding(int kind, QualifiedName qualifiedAxiomName, ParserAssembler parserAssembler) :
{
  Token nameToken = null;
}
{
  ( nameToken=<STRING_LITERAL> | <RESOURCE> )
  {
    QualifiedName qualifiedBindingName = 
        kind == QueryParserConstants.TEMPLATE ?
        new QualifiedTemplateName(qualifiedAxiomName.getScope(), qualifiedAxiomName.getName()) :
        qualifiedAxiomName;
    AxiomProvider axiomProvider = (nameToken != null) ?
        parserAssembler.bindResource(QualifiedName.parseName(getText(nameToken)), qualifiedBindingName) :
        parserAssembler.bindResource(qualifiedBindingName);
    return qualifiedBindingName;
  }
}

void AxiomItem(QualifiedName qualifiedAxiomName, ParserAssembler parserAssembler) :
{
}
{
  Fact(qualifiedAxiomName, parserAssembler) ("," Fact(qualifiedAxiomName, parserAssembler) )*  
  {
    parserAssembler.saveAxiom(qualifiedAxiomName);
  }
}

void Fact(QualifiedName qualifiedAxiomName, ParserAssembler parserAssembler) :
{
  Parameter param = null;
  Token lit = null;
}
{
  param=LiteralTerm()
  {
    parserAssembler.addAxiom(qualifiedAxiomName, param);
  }
  |
  lit=<NUMBER_LITERAL>
  {
    parserAssembler.addAxiom(qualifiedAxiomName, new NumberTerm(getText(lit), parserAssembler.getScopeLocale()));
  }
  |
  <NAN>
  {
    parserAssembler.addAxiom(qualifiedAxiomName, new DoubleTerm("NaN"));
  }
}

Operand AxiomVariable(QualifiedName qualifiedAxiomName, ParserContext context) :
{
  Operand expression = null;
  List<OperandParam> axiomList = null;
  ParserAssembler parserAssembler = context.getParserAssembler();
}
{
  "="
  ( 
     expression=Expression(parserAssembler) 
  | 
     axiomList=AxiomList(qualifiedAxiomName, parserAssembler) 
  )
  {
     VariableType varType = new VariableType(axiomList == null ? OperandType.AXIOM : OperandType.LIST);
     if (expression != null)
         varType.setProperty(VariableType.EXPRESSION, expression);
     else
         varType.setProperty(VariableType.PARAMS, axiomList);
     Operand operand = varType.getInstance(parserAssembler, qualifiedAxiomName);
     if (expression != null)
         parserAssembler.setParameter(operand.getQualifiedName());
     return operand;
  }
} 

List<OperandParam> AxiomList(QualifiedName qualifiedAxiomName, ParserAssembler parserAssembler) :
{
  List<OperandParam> operandParamList = new ArrayList<OperandParam>();
  Operand operand;
}
{
  operand = AxiomInitializer(qualifiedAxiomName.getName(), qualifiedAxiomName, parserAssembler)
  {
    operandParamList.add(new OperandParam(qualifiedAxiomName.getName(), operand));
  }
  ( 
    operand = AxiomInitializer(qualifiedAxiomName.getName(), qualifiedAxiomName, parserAssembler)
    {
      operandParamList.add(new OperandParam(qualifiedAxiomName.getName(), operand));
    }
  )*
  {
    return operandParamList;
  }
}

Operand AxiomInitializer(String listName, QualifiedName axiomName, ParserAssembler parserAssembler) :
{
  List<OperandParam> initializeList = null;
}
{
  <LBRACE> ( initializeList = ArgumentList(parserAssembler, true) )? <RBRACE>
  {
     VariableType varType = new VariableType(OperandType.TERM);
     varType.setProperty(VariableType.AXIOM_KEY, axiomName);
     if (initializeList != null)
        varType.setProperty(VariableType.PARAMS, initializeList);
     Operand operand = varType.getInstance(parserAssembler, listName);
     return operand;
  }
}

void TemplateDeclaration(ParserContext context) :
{
  Template template;
  Operand expression;
  QualifiedName contextName = context.getContextName();
}
{
  <TEMPLATE> template=Template(context, false) "(" 
  ( 
    expression=TemplateExpression(template, context)
    {
        template.addTerm(expression);
    }
  )
  (
    "," 
    (
      expression=TemplateExpression(template, context)
      {
        template.addTerm(expression);
      }
    )
  )* ")" 
  {
    context.setContextName(contextName);
  }
}

Template Template(ParserContext context, boolean isCalculator) :
{
  Token templateToken;
}
{
  templateToken=<IDENTIFIER>
  { 
    QualifiedName qualifiedTemplateName = new QualifiedTemplateName(context.getScope().getAlias(), templateToken.image);
    context.setContextName(qualifiedTemplateName);
    return context.getParserAssembler().createTemplate(qualifiedTemplateName, isCalculator); 
  }
}

void CalculatorTemplate(ParserContext context) :
{
  Template template;
  Map<String, Object> properties = new HashMap<String, Object>();
  int loopNumber = 0;
  ParserAssembler parserAssembler = context.getParserAssembler();
  QualifiedName contextName = context.getContextName();
}
{  
  <CALC> template=Template(context, true) "(" 
  (  
    ( CalculatorExpression(loopNumber, template, template.getName(), context) |
      CalculatorQuery(template, context))
    )
    (
      "," 
      (  
        ( CalculatorExpression(loopNumber, template, template.getName(), context) |
          CalculatorQuery(template, context))
      )
    )* ")" [ "(" InitialiserList(properties) ")" ]
  {
    if (properties.size() > 0)
        template.addProperties(properties);
    context.setContextName(contextName);
  }
}

Operand TemplateExpression(Template template, ParserContext context) :
{
    String name;
    VariableType varType = null;
    Operand index = null;
    Operand var = null;
    Token scToken = null;
    Token privateToken = null;
    Token assignToken = null;
    Token equalsToken = null;
    Token regexLit = null;
    Token regexId = null;
    Operand expression = null;
    Group group = null;
    ParserAssembler parserAssembler = context.getParserAssembler();
    OperandMap operandMap = parserAssembler.getOperandMap();
    List<Parameter> literalList = null;
    List<OperandParam> axiomList = null;
    QualifiedName qname = null;
    List<OperandParam> operandParamList = null;
}
{
  ( varType=Type(operandMap) )? name=Name() 
  {
    qname = parserAssembler.getContextName(name);
  }
  ( index=IndexExpression(parserAssembler) )? 
  (
     ( 
       ( equalsToken="=" | assignToken=<PLUSASSIGN> )
       ( <AXIOM> axiomList=AxiomList(qname, parserAssembler) 
       {
         VariableType axiomListVarType = new VariableType(OperandType.LIST);
         axiomListVarType.setProperty(VariableType.PARAMS, axiomList);
         expression = axiomListVarType.getInstance(parserAssembler, qname);
       } 
       |
       expression=Expression(parserAssembler) )
    )
    |
    (  
       ( scToken=":" | scToken="?" 
       | assignToken=<MINUSASSIGN>
       | assignToken=<STARASSIGN>
       | assignToken=<SLASHASSIGN>
       | assignToken=<ANDASSIGN>
       | assignToken=<ORASSIGN>
       | assignToken=<XORASSIGN>
       | assignToken=<REMASSIGN> )
       expression=Expression(parserAssembler) 
    )
    |
        <REGEX> "(" ( regexLit=<STRING_LITERAL> | regexId=<IDENTIFIER> ) ( group=GroupDeclaration(template, name, parserAssembler) )? ")"
    |
        <LBRACE> literalList = LiteralList() <RBRACE> 
    |
     "(" ( operandParamList = ArgumentList(parserAssembler, true) )? ")"
	{
	     template.addTerm(parserAssembler.getCallOperand(qname, operandParamList));
	}
  )?
  {
     if (varType == null)
       varType = new VariableType(OperandType.UNKNOWN);
     Operand assignExpression = (assignToken != null) ? expression : null;
     Operand regexOp = null;
     if (regexLit != null)
       regexOp = new StringOperand(QualifiedName.ANONYMOUS, getText(regexLit));
     else if (regexId != null)
       regexOp = operandMap.addOperand(regexId.image, null);
     if (assignToken != null)
       expression = null;
     boolean isLiteral = (expression != null) && !expression.isEmpty() && (equalsToken != null);
     if (expression != null)
       varType.setProperty(isLiteral ? VariableType.LITERAL : VariableType.EXPRESSION, expression);
     if (index !=null)
       var = listItemOperand(name, parserAssembler, index, null);
     else if (regexOp != null)
       var = new RegExOperand(qname, regexOp, 0, group);
     else if (scToken != null) 
       var = new Evaluator(qname, expression, (scToken.image == "?" ? "&&" : "||"));
     else if (literalList != null)
       var = new LiteralListOperand(qname, literalList);
     else if (!operandMap.hasOperand(name))
       var = varType.getInstance(parserAssembler, name);
     else
       var = operandMap.addOperand(name, expression);
     if ((index == null) && (!operandMap.hasOperand(name)))
       operandMap.addOperand(var);
     if (assignToken != null) 
       var = new Evaluator(qname, var, assignToken.image, assignExpression);
     if ((index !=null) && (equalsToken != null) && !isLiteral) 
       var = new Evaluator(parserAssembler.getContextName(expression.getName()), var, "=", expression);
      return var;
  }
}

void CalculatorExpression(int loop_number, Template template, String templateName, ParserContext context) :
{
  Token scToken = null;
  Token privateToken = null;
  Operand expression = null;
  Operand innerLoop = null;
  QualifiedName qualifiedAxiomName;
  QualifiedName qualifiedTemplateName;
  ParserAssembler parserAssembler = context.getParserAssembler();
}
{
  ( scToken=":" | scToken="?" ) expression=Expression(parserAssembler)
    ( innerLoop=InnerCalculator(loop_number + 1, templateName, context, scToken != null) )?  
  {
    if (scToken != null)
      expression = new Evaluator(expression, (scToken.image == "?" ? "&&" : "||"), innerLoop);
    if (scToken != null)
      expression.setPrivate(true);
    if (privateToken != null)
        expression.setPrivate(true);
    template.addTerm(expression);
  }
  |
  ( privateToken="." )? (expression=TemplateExpression(template, context) |
                         expression=Expression(parserAssembler))
  {
    if (privateToken != null)
      expression.setPrivate(true);
    template.addTerm(expression);
  }
  |
  innerLoop=InnerCalculator(loop_number + 1, templateName, context, false)
  {
    template.addTerm(innerLoop);
  }
  |
  <AXIOM> qualifiedAxiomName=Axiom(context) expression = AxiomVariable(qualifiedAxiomName, context) 
  {
    parserAssembler.getOperandMap().addOperand(expression);
    template.addTerm(expression);
  }
  |
  qualifiedAxiomName = ChoiceDeclaration(context)
  {
    qualifiedTemplateName = new QualifiedTemplateName(qualifiedAxiomName.getScope(), qualifiedAxiomName.getName());
    Template choiceTemplate = parserAssembler.getTemplate(qualifiedTemplateName);
    QualifiedName contextName = context.getContextName();
    QualifiedName qname = QualifiedName.parseName(choiceTemplate.getName(), contextName); 
 	Choice choice = new Choice(qualifiedAxiomName, parserAssembler.getScope());
    Operand choiceOperand = new ChoiceOperand(qname, choiceTemplate, choice);
    template.addTerm(choiceOperand);
  }
}

void CalculatorQuery(Template template, ParserContext context) :
{
  String queryName;
  List<OperandParam> operandParamList = null;
  Template innerTemplate = null;
}
{
  ( innerTemplate=InnerTemplateDeclaration(template.getQualifiedName(), context) )? "<<" queryName=Name()  
   "(" ( operandParamList = ArgumentList(context.getParserAssembler(), false) )? ")"
  {
    QualifiedName qname = QualifiedName.parseName(queryName);
    Operand queryOperand = context.getParserAssembler().getQueryOperand(qname, operandParamList, innerTemplate);
    template.addTerm(queryOperand);
    if (innerTemplate != null)
        template.setNext(innerTemplate);
  }
}

Template InnerTemplateDeclaration(QualifiedName ownerQualifiedName, ParserContext context) :
{
  Template template;
  Operand expression;
}
{
    <TEMPLATE> template=InnerTemplate(ownerQualifiedName, context) "(" 
      ( 
        expression=TemplateExpression(template, context)
        {
            template.addTerm(expression);
        }
      )
      (
        "," 
        (
          expression=TemplateExpression(template, context)
        {
            template.addTerm(expression);
        }
        )
      )* ")" 
    {
        return template;
    }
}

Template InnerTemplate(QualifiedName ownerQualifiedName, ParserContext context) :
{
  Token templateToken;
}
{
  templateToken=<IDENTIFIER>
  { 
    QualifiedName qualifiedTemplateName = new QualifiedName(templateToken.image, ownerQualifiedName);
    Template innerTemplate = context.getParserAssembler().createTemplate(qualifiedTemplateName, false);
    innerTemplate.setInnerTemplate(true);
    innerTemplate.setKey(templateToken.image);
    return innerTemplate; 
  }
}

QualifiedName Choice(ParserAssembler parserAssembler) :
{
    Token choiceToken;
}
{
  choiceToken=<IDENTIFIER>
  { 
    QualifiedName qualifiedChoiceName = parserAssembler.getContextName(choiceToken.image);
    parserAssembler.createAxiom(qualifiedChoiceName);
    QualifiedName qualifiedTemplateName = new QualifiedTemplateName(qualifiedChoiceName.getScope(), qualifiedChoiceName.getName());
    Template template = parserAssembler.createTemplate(qualifiedTemplateName, true);
    template.setChoice(true);
    return qualifiedChoiceName;
  }
}
 
Operand ChoiceExpression(String name, ParserAssembler parserAssembler) :
{
    Operand operand;
    QualifiedName qname = parserAssembler.getContextName(name);
}
{
    operand=Expression(parserAssembler)
    {
      if (operand instanceof Evaluator)
          return new Evaluator(qname, operand, "&&" ); 
      if (operand instanceof StringOperand)
          return new RegExOperand(qname, operand, 0, null);
      return new MatchOperand(qname, operand);
    }
}

Operand InnerCalculator(int loop_number, String templateName, ParserContext context, boolean runOnce) :
{
  String loopName = templateName + loop_number;
  QualifiedName qualifiedTemplateName = new QualifiedTemplateName(context.getScope().getAlias(), templateName);
  Template template = context.getParserAssembler().chainTemplate(qualifiedTemplateName, loopName);
}
{
  "{" 
    CalculatorExpression(loop_number, template, templateName, context)
    ("," 
        CalculatorExpression(loop_number, template, templateName, context)
    )* 
  "}"
  {
    return new LoopEvaluator(template, runOnce);
  }
}

QualifiedName ChoiceDeclaration(ParserContext context) :
{
    QualifiedName qualifiedAxiomName;
    int selection = 0;
    ParserAssembler parserAssembler = context.getParserAssembler();
    OperandMap operandMap = parserAssembler.getOperandMap();
}
{
  <CHOICE> qualifiedAxiomName=Choice(parserAssembler) "(" TermName(qualifiedAxiomName, parserAssembler) ("," TermName(qualifiedAxiomName, parserAssembler) )*  ")" 
                                                        ( ChoiceItem(selection, qualifiedAxiomName, parserAssembler) )+ 
  {
      QualifiedName contextName = context.getContextName();
      if (contextName.getTemplate().isEmpty())
          return qualifiedAxiomName;
      List<String> termNameList = parserAssembler.getAxiomTermNameList(qualifiedAxiomName);
      for (String termName: termNameList)
          operandMap.addOperand(termName, null);
      context.setContextName(contextName);
      return qualifiedAxiomName;
  }
} 

void ChoiceItem(int selection, QualifiedName qualifiedAxiomName, ParserAssembler parserAssembler) :
{
    Operand operand;
    String name = parserAssembler.getAxiomTermName(qualifiedAxiomName, 0);
    parserAssembler.addAxiom(qualifiedAxiomName, new Parameter(Term.ANONYMOUS, new Null()));
}
{
   <LBRACE> operand=ChoiceExpression(name, parserAssembler) ("," Fact(qualifiedAxiomName, parserAssembler) )* <RBRACE> 
    {
       parserAssembler.saveAxiom(qualifiedAxiomName);
       QualifiedName qualifiedTemplateName = new QualifiedTemplateName(parserAssembler.getScope().getAlias(), qualifiedAxiomName.getName());
       parserAssembler.addTemplate(qualifiedTemplateName, operand);
    }
}

void InitialiserList(Map<String, Object> properties) :
{
}
{
    InitialiserDeclaration(properties)
    ("," InitialiserDeclaration(properties) )* 
}

void InitialiserDeclaration(Map<String, Object> properties) :
{
    String name;
    Parameter param;
}
{
  name=Name() "=" param=LiteralTerm()
  {
     properties.put(name, param.getValue());
  }
}

Group GroupDeclaration(Template template, String name, ParserAssembler parserAssembler) :
{
  Group group = new Group(name);
}
{
  "{" Group(group, template, name, parserAssembler) ("," Group(group, template, name, parserAssembler) )* "}"
  {
    return group;
  }
}

 
void Group(Group group, Template template, String name, ParserAssembler parserAssembler) :
{
  Token groupToken;
}
{
  groupToken=<IDENTIFIER> 
  {
    Operand var = parserAssembler.getOperandMap().addOperand(groupToken.image, null);   
    template.addTerm(var);
    group.addGroup(var);
  }
}

List<Parameter> LiteralList() :
{
  List<Parameter>  literalList = new ArrayList<Parameter>();
  Parameter parameter;
}
{
  parameter=LiteralTerm()
  {
    literalList.add(parameter);
  }
  ( 
    "," 
    parameter = LiteralTerm() 
    {
      literalList.add(parameter);
    }
  )*
  {
    return literalList;
  }
}

Operand VariableDeclaration(ParserContext context) :
{
  Token nameToken;
  VariableType varType = null;
  Operand index = null;
  Operand expression = null;
  ParserAssembler parserAssembler = context.getParserAssembler();
  OperandMap operandMap = parserAssembler.getOperandMap();
}
{
  ( varType=Type(operandMap) )? nameToken=<IDENTIFIER> ( index=IndexExpression(parserAssembler) )? ( "=" expression=Expression(parserAssembler) )?
  {
     String name = nameToken.image;
     if (index !=null)
         return listItemOperand(name, parserAssembler, index, expression);
     if (varType == null)
         varType = new VariableType(OperandType.UNKNOWN);
     boolean isLiteral = (expression != null) &&
                          !expression.isEmpty() && !(expression instanceof Evaluator);
     if (expression != null)
         varType.setProperty(isLiteral ? VariableType.LITERAL : VariableType.EXPRESSION, expression);
     Operand operand = varType.getInstance(parserAssembler, name);
     return operand;
  }
}

void ListDeclaration(ParserContext context) :
{
  Token keywordToken;
  Token nameToken;
  VariableType varType = null;
  QualifiedName qualifiedBindingName = null;
  QualifiedName qualifiedAxiomName = null;
  ParserAssembler parserAssembler = context.getParserAssembler();
}
{
  ( keywordToken=<LIST> | keywordToken=<LOCAL> ) 
  [ "<" varType=Type(parserAssembler.getOperandMap()) ">" ] 
  nameToken=<IDENTIFIER> 
  [ "(" qualifiedAxiomName=Axiom(context)  ( ":" qualifiedBindingName = ResourceBinding(QueryParserConstants.TEMPLATE, qualifiedAxiomName, parserAssembler) )? ")" ]
  {
    String listName = nameToken.image;
    boolean isLocal = keywordToken.kind == QueryParserConstants.LOCAL;
    boolean isTermList = (varType != null) && varType.getOperandType() == OperandType.TERM;
    if ((varType == null) && (qualifiedAxiomName == null))
        throw new ParseException("Invalid declaration for list \"" + listName + "\". Missing type or axiom name.");
    if ((varType != null) && isLocal)
        throw new ParseException("Invalid declaration for local \"" + listName + "\". Type in declaration not allowed.");
    if ((qualifiedBindingName != null) && !isTermList)
        throw new ParseException("Invalid declaration for " + keywordToken.image + " \"" + listName + "\". Only Term type list can be bound to a resource");
    if (qualifiedBindingName != null)
        qualifiedAxiomName = qualifiedBindingName;
    if (varType == null)
        varType = isLocal ? new VariableType(OperandType.LOCAL) : new VariableType(OperandType.AXIOM);
    if (qualifiedAxiomName != null)
        varType.setProperty(VariableType.AXIOM_KEY, qualifiedAxiomName);
    ItemList<?> itemList = varType.getItemListInstance(parserAssembler, listName);
    parserAssembler.getOperandMap().addItemList(itemList.getQualifiedName(), itemList);
  }
}

void Include(ParserContext context) :
{
  Token includeToken = null;
}
{
  <INCLUDE> includeToken=<STRING_LITERAL>
  {
    includeResource(includeToken.image, context.getQueryProgram());
  }
}

Operand Expression(ParserAssembler parserAssembler) :
{
  Operand param;
  Token assignToken = null;
  Operand assignOperand = null;
}
{
  param=ConditionalOrExpression(parserAssembler) 
    ( ( assignToken=<ASSIGN>
        | assignToken=<PLUSASSIGN>
        | assignToken=<MINUSASSIGN>
        | assignToken=<STARASSIGN>
        | assignToken=<SLASHASSIGN>
        | assignToken=<ANDASSIGN>
        | assignToken=<ORASSIGN>
        | assignToken=<XORASSIGN>
        | assignToken=<REMASSIGN>
      )
      assignOperand=Expression(parserAssembler) )?
  {
    if (assignOperand == null)
      return param;
    return new Evaluator(param, assignToken.image, assignOperand);
  }
}

Operand PrimaryExpression(ParserAssembler parserAssembler) :
{
  String name = null;
  QualifiedName qname;
  Token literal = null;
  Operand param1 = null;
  Operand operand;
  OperandMap operandMap = parserAssembler.getOperandMap();
}
{
  param1=Literal()
  {
    return param1;
  }
  |
  literal=<NUMBER_LITERAL>
  {
    NumberTerm numberTerm = new NumberTerm(getText(literal), parserAssembler.getScopeLocale());
    Variable var = new Variable(QualifiedName.ANONYMOUS);
    var.assign(numberTerm);
    return var;
  }
  |
  name=Name() ( param1=NamedExpression(name, parserAssembler) )?
  {
    if (param1 == null)
        return operandMap.addOperand(name, null);
    return param1;
  }
  |
  <SCOPE> param1=IndexExpression(parserAssembler)
  {
    return listItemOperand("scope", parserAssembler, param1, null);
  }
  |
  "(" ( param1 = Expression(parserAssembler) ) ")" 
  {
    return param1;
  }
  |
  <LENGTH> "("  name=Name() ")"
  {
    qname = parserAssembler.getContextName(name);
    operand = parserAssembler.findOperandByName(name);
    if (operand != null)
        return new ListLength(qname, operand);
    else 
        return new ListLength(qname, parserAssembler.getItemList(name));
  }
  |
  <FORMAT> "("  name=Name() ")"
  {
    qname = parserAssembler.getContextName(name + "_format");
    operand = parserAssembler.findOperandByName(name);
    if (operand == null)
        throw new ParseException("Variable \"" + name + "\" not found");
    Scope scope = parserAssembler.getScope();
    FormatterOperand formatter = new FormatterOperand(qname, operand, parserAssembler.getScopeLocale());
    if (scope.getName().equals(QueryProgram.GLOBAL_SCOPE))
        parserAssembler.registerLocaleListener(formatter);
    return formatter;
  }
  |
  <FACT> "("  name=Name() ")"
  {
    Operand factOperand = parserAssembler.findOperandByName(name);
    if (factOperand != null)
        return new FactOperand(factOperand);
    else 
        throw new ParseException("Operand " + name + " not found");
  }
}

List<OperandParam> ArgumentList(ParserAssembler parserAssembler, boolean nameRequired) :
{
  List<OperandParam> operandParamList = new ArrayList<OperandParam>();
  OperandParam operandParam;
}
{
  operandParam = Argument(parserAssembler, nameRequired)
  {
    operandParamList.add(operandParam);
  }
  ( 
    "," 
    operandParam=Argument(parserAssembler, nameRequired) 
    {
      operandParamList.add(operandParam);
    }
  )*
  {
    return operandParamList;
  }
}

OperandParam Argument(ParserAssembler parserAssembler, boolean nameRequired) :
{
  VariableType varType = null;
  Operand expression;
  OperandMap operandMap = parserAssembler.getOperandMap();
}
{
  (varType=Type(operandMap))? expression=Expression(parserAssembler) 
  {
     boolean isAssign = false;
     Operand leftOperand = expression.getLeftOperand();
     if ((leftOperand != null) && (expression instanceof Evaluator))
       isAssign =  ((Evaluator)expression).getOperator() == OperatorEnum.ASSIGN; 
     String name = Term.ANONYMOUS;
     if (nameRequired)
       name = expression.getName();
     if (!isAssign) 
       return new OperandParam(name, expression);
     name = leftOperand.getName();
     expression = expression.getRightOperand();
     boolean isLiteral = !expression.isEmpty();
     if (varType == null)
       varType = new VariableType(OperandType.UNKNOWN);
     if (varType.getOperandType() == OperandType.TERM)
       varType.setUnknownType();
     varType.setProperty(isLiteral ? VariableType.LITERAL : VariableType.EXPRESSION, expression);
     Operand operand = varType.getInstance(parserAssembler, name);
     return new OperandParam(name, operand);
  }
}

Operand IndexExpression(ParserAssembler parserAssembler) :
{
  Operand param;
}
{
  <LBRACKET> param=Expression(parserAssembler) <RBRACKET> 
  {
    return param;
  }
}

Operand Literal() :
{
  Token lit;
  boolean flag;
}
{
  lit=<INTEGER_LITERAL>
  {
    Long litValue = Long.decode(lit.image);
    return new IntegerOperand(QualifiedName.ANONYMOUS, litValue);
  }
  |
  lit=<FLOATING_POINT_LITERAL>
  {
    return new DoubleOperand(QualifiedName.ANONYMOUS, Double.valueOf(lit.image));
  }
  |
  lit=<STRING_LITERAL>
  {
    return new StringOperand(QualifiedName.ANONYMOUS, getText(lit));
  }
  |
  flag=BooleanLiteral()
  {
    return new BooleanOperand(QualifiedName.ANONYMOUS, flag);
  }
  |
  UnknownLiteral()
  {
    return new NullOperand(QualifiedName.ANONYMOUS, new Unknown());
  }
}

VariableType Type(OperandMap operandMap) :
{
  Token qualifierLit = null;
  String qualifierId = null;
}
{
    <INTEGER>
    { 
      return new VariableType(OperandType.INTEGER); 
    }
|   <BOOLEAN>
    { 
      return new VariableType(OperandType.BOOLEAN); 
    }
|   <DOUBLE>
    { 
      return new VariableType(OperandType.DOUBLE); 
    }
|   <STRING>
    { 
      return new VariableType(OperandType.STRING); 
    }
|   <DECIMAL>
    { 
      return new VariableType(OperandType.DECIMAL); 
    }
|   <TERM>
    { 
      return new VariableType(OperandType.TERM); 
    }
|   <CURRENCY> ( "$" ( qualifierLit=<STRING_LITERAL> | qualifierId=Name() ))?
    { 
      VariableType varType = new VariableType(OperandType.CURRENCY);
      if (qualifierLit != null)
         varType.setProperty(VariableType.QUALIFIER_STRING, getText(qualifierLit));
      else if (qualifierId != null)
         varType.setProperty(VariableType.QUALIFIER_OPERAND, operandMap.addOperand(qualifierId, null));
      return varType; 
    }

}

Operand CalculatorFunction(ParserAssembler parserAssembler) :
{
    String fnName;
    List<OperandParam> operandParamList = null;
}
{
  fnName=Name() "(" ( operandParamList = ArgumentList(parserAssembler, true) )? ")"
  {
    return parserAssembler.getCallOperand(parserAssembler.getContextName(fnName), operandParamList);
  }
}

void ParameterDeclaration(QualifiedName qualifiedAxiomName, ParserAssembler parserAssembler) :
{
}
{
  <PARAMETER>
  {
    parserAssembler.setParameter(qualifiedAxiomName);
  }
}

String Name() :
{
  String name;
  Token partToken;
}
{
  partToken=<IDENTIFIER> 
  {
    name = partToken.image;
  }
  ( 
    "." 
    partToken=<IDENTIFIER>
    {
      name += ("." + partToken.image);
    } 
  )*
  {
    return name;
  }
}

Operand NamedExpression(String name, ParserAssembler parserAssembler) :
{
  Operand param1 = null;
  Operand param2 = null;
  List<OperandParam> operandParamList = null;
  QualifiedName qname;
}
{
  param1=IndexExpression(parserAssembler) ( LOOKAHEAD(2) param2=IndexExpression(parserAssembler) )?
  {
    return axiomOperand(name, parserAssembler, param1, param2);
  }
  |
  "(" ( operandParamList = ArgumentList(parserAssembler, true) )? ")"
  {
    qname = parserAssembler.getContextName(name);
    return parserAssembler.getCallOperand(qname, operandParamList);
  }
}

Parameter LiteralTerm() :
{
  Token lit;
  boolean flag;
}
{
  lit=<INTEGER_LITERAL>
  {
    return new IntegerTerm(lit.image);
  }
  |
  lit=<FLOATING_POINT_LITERAL>
  {
    return new DoubleTerm(lit.image);
  }
  |
  lit=<STRING_LITERAL>
  {
    return new StringTerm(getText(lit));
  }
  |
  flag=BooleanLiteral()
  {
    return new BooleanTerm(flag);
  }
  |
  UnknownLiteral()
  {
    return new Parameter(Term.ANONYMOUS, new Unknown());
  }
}

boolean BooleanLiteral() :
{}
{
  <TRUE> 
  { 
    return true; 
  }
  |
  <FALSE> 
  { 
    return false; 
  }
}

void UnknownLiteral() :
{}
{
  <UNKNOWN>
}

Operand ConditionalOrExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=ConditionalAndExpression(parserAssembler)
  ( 
    op="||" 
    params[1]=ConditionalAndExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  } 
}

Operand ConditionalAndExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=InclusiveOrExpression(parserAssembler)
  ( 
    op="&&" 
    params[1]=InclusiveOrExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  }
}

Operand InclusiveOrExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=ExclusiveOrExpression(parserAssembler)
  ( 
    op="|" 
    params[1]=ExclusiveOrExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  }
}

Operand ExclusiveOrExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=AndExpression(parserAssembler)
  ( 
    op="^" 
    params[1]=AndExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  }
}

Operand AndExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=EqualityExpression(parserAssembler)
  ( 
    op="&" 
    params[1]=EqualityExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  }
}

Operand EqualityExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=RelationalExpression(parserAssembler)
  ( 
    ( 
    op="==" 
    | 
    op="!=" 
    ) 
    params[1]=RelationalExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  }
}

Operand RelationalExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=AdditiveExpression(parserAssembler)
  ( 
    ( 
    op="<" 
    | 
    op=">" 
    |
    op="<=" 
    | 
    op=">=" 
    ) 
    params[1]=AdditiveExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  }
}

Operand AdditiveExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=MultiplicativeExpression(parserAssembler)
  ( 
    ( 
    op="+" 
    | 
    op="-" 
    ) 
    params[1]=MultiplicativeExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  }
}

Operand MultiplicativeExpression(ParserAssembler parserAssembler) :
{
  Operand[] params = new Operand[2];
  Token op;
}
{
  params[0]=UnaryExpression(parserAssembler)
  ( 
    ( 
    op="*" 
    | 
    op="/" 
    | 
    op="%" 
    ) 
    params[1]=UnaryExpression(parserAssembler) 
    {
      params[0] = new Evaluator(params[0], op.image, params[1]);
    }
  )*
  {
    return params[0];
  } 
}

Operand UnaryExpression(ParserAssembler parserAssembler) :
{
  Operand param;
  boolean plus = false;
  boolean minus = false;
  boolean tilde = false;
}
{
  ( 
    "+"
  {
    plus = true;
  }
  | 
    "-"
  {
    minus = true;
  }
  |
    "~"
  {
    tilde = true;
  }
  ) 
  param=UnaryExpression(parserAssembler)
  {
    if (plus)
      return new Evaluator("+", param);
    else if (minus)
      return new Evaluator("-", param);
    else if (tilde)
      return new Evaluator("~", param);
    return param;
  }
  |
  param=PreIncrementExpression(parserAssembler)
  {
    return param;
  }
  |
  param=PreDecrementExpression(parserAssembler)
  {
    return param;
  }
  |
  param=UnaryExpressionNotPlusMinus(parserAssembler)
  {
    return param;
  }
}

Operand PreIncrementExpression(ParserAssembler parserAssembler) :
{
  Operand param;
}
{
  "++" param=PrimaryExpression(parserAssembler)
  {
    return new Evaluator("++", param);
  }
}

Operand PreDecrementExpression(ParserAssembler parserAssembler) :
{
  Operand param;
}
{
  "--" param=PrimaryExpression(parserAssembler)
  {
    return new Evaluator("--", param);
  }
}

Operand UnaryExpressionNotPlusMinus(ParserAssembler parserAssembler) :
{
  Operand param;
}
{
  "!" param=UnaryExpression(parserAssembler)
  {
    return new Evaluator("!", param);
  }
  |
  param=PostfixExpression(parserAssembler)  
  {
    return param;
  }
}

Operand PostfixExpression(ParserAssembler parserAssembler) :
{
  Operand param;
  boolean incr = false;
  boolean decr = false;
}
{
  param=PrimaryExpression(parserAssembler)
  [ 
    "++"
  {
    incr = true;
  }
  | 
    "--"
  {
    decr = true; 
  }
  ]
  {
    if (incr)
       return new Evaluator(param, "++");
    else if (decr)
       return new Evaluator(param, "--");
    return param;
  } 
}

